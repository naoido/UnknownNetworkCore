buildscript {
    dependencies {
        classpath files('libs/gradle/run-paper.jar')
    }
}

plugins {
    id 'java'
    id 'io.papermc.paperweight.userdev' version '+' // Enable Development of using Mojang Mapping.
    //id 'xyz.jpenilla.run-paper' version '+' // Run Mojang Mapped or Spigot Mapped Server
    id 'com.github.johnrengelman.shadow' version '7.1.0' // fatJar
    id 'org.cadixdev.licenser' version '0.6.1' // Add License Header to source code headers.
    id "de.undercouch.download" version "4.1.2" // File Downloader
}

apply plugin: xyz.jpenilla.runpaper.RunPaper

group 'net.unknown'
version '1.0-Alpha'

compileJava.dependsOn = ["licenseFormat"]
compileJava.options.encoding = "UTF-8"
javadoc.options.encoding = "UTF-8"
processResources.filteringCharset = "UTF-8"

java.sourceCompatibility = JavaVersion.VERSION_17
java.targetCompatibility = JavaVersion.VERSION_17
compileJava.options.release.set(17)

tasks.build.dependsOn(reobfJar)

license {
    header = project.file("HEADER.txt")
    include '**/*.java'
    properties {
        organization = 'Unknown Network'
        year = 2022
    }
    newLine = true
}

repositories {
    mavenCentral()
    maven { url = "https://repo.codemc.io/repository/maven-public/" }
    maven { url = "https://repo.spongepowered.org/maven/" }
    maven { url = "https://files.minecraftforge.net/maven/" }
    maven { url = "https://maven.quiltmc.org/repository/release/" }
    maven { url = "https://maven.enginehub.org/repo/" }
    maven { url = "https://jitpack.io" }
}

dependencies {
    paperweightDevelopmentBundle("io.papermc.paper:dev-bundle:1.18.1-R0.1-SNAPSHOT")
    compileOnly fileTree(dir: "libs", include: "*.jar")
    compileOnly group: 'com.sk89q.worldguard', name: 'worldguard-bukkit', version: '+'
    compileOnly group: 'me.filoghost.holographicdisplays', name: 'holographicdisplays-plugin', version: '+'
    compileOnly group: 'com.github.MilkBowl', name: 'VaultAPI', version: '+'
    compileOnly group: 'net.luckperms', name: 'api', version: '+'
    implementation group: 'org.mozilla', name: 'rhino', version: '+'
}

shadowJar {
}

runMojangMappedServer {
    runDirectory(project.file("testServer"))
    setSystemProperty("net.kyori.adventure.text.warnWhenLegacyFormattingDetected", "false")
    setSystemProperty("Paper.ignoreWorldDataVersion", "true")
    setSystemProperty("un.env", System.getProperty("un.env", "SURVIVAL"))
    args("-P pluginsMapped")
}

runServer {
    runDirectory.set(project.file("testServer"))
    setSystemProperty("net.kyori.adventure.text.warnWhenLegacyFormattingDetected", "false") // default is false, but run-paper is set this option force true.
    setSystemProperty("Paper.ignoreWorldDataVersion", "true")
    setSystemProperty("un.env", System.getProperty("un.env", "SURVIVAL"))
}

task downloadWaterfall(type: Task) {
    var wf = new File("libs/standalone/waterfall.jar")

    if (!wf.exists()) {
        download {
            src "https://papermc.io/api/v2/projects/waterfall/versions/1.18/builds/471/downloads/waterfall-1.18-471.jar"
            dest 'libs/standalone/waterfall.jar'
        }

        println "Downloaded waterfall.jar"
    }

    if (wf.exists()) {
        if(!file("libs/waterfall").exists()) {
            copy {
                from zipTree(file("libs/standalone/waterfall.jar"))
                into file("libs/waterfall/")
            }

            println "Extracted waterfall"
        }

        if(file("libs/waterfall/com/mojang/brigadier").exists()) {
            delete {
                delete file("libs/waterfall/com/mojang/brigadier")
            }

            println "Removed brigadier from extracted waterfall"
        }
    }
}

task reCompressWaterfall(type: Zip, dependsOn: downloadWaterfall) {
    from "libs/waterfall/"
    destinationDirectory = file("libs")
    archiveFileName = "waterfall.jar"
}